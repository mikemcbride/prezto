#
# Based on zeit.zsh-theme
# http://github.com/zeit/zeit.zsh-theme
#

function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}●%f'
  fi
}

function prompt_zeit_precmd {
  vcs_info
  
  local prefix="%F{white}▲%f"
  local dir="%F{white}%2~%f"
  local branch="%F{yellow}$(prompt_zeit_git)%f"
  
  # Define prompts.
  PROMPT="$prefix $dir $branch"
  RPROMPT=''
}

# Git: branch/detached head, dirty status
prompt_zeit_git() {
  local ref dirty
  if [[ -n ${vcs_info_msg_0_} ]]; then
    # vcs_info found something (the documentation got that backwards)
    # STATUS line taken from https://github.com/robbyrussell/oh-my-zsh/blob/master/lib/git.zsh
    STATUS=$(command git status --porcelain --ignore-submodules -unormal 2> /dev/null | tail -n1)
    
    if [[ -n $STATUS ]]; then
      dirty='*'
    else
      dirty=''
    fi
    
    if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
      ref=$(git symbolic-ref HEAD 2> /dev/null) || ref="➦ $(git show-ref --head -s --abbrev |head -n1 2> /dev/null)"
      echo -n "(${ref/refs\/heads\//}$dirty) "
    fi
  else
    # nothing from vcs_info
    echo -n
  fi
}

function prompt_zeit_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_zeit_precmd

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' formats ' - [%b%c%u]'
  zstyle ':vcs_info:*' formats ' %b' 'x%R'
	zstyle ':vcs_info:*' actionformats ' %b|%a' 'x%R'
  zstyle ':vcs_info:git*+set-message:*' hooks git_status
}

prompt_zeit_setup "$@"
